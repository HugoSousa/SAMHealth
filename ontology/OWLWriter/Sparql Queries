//ALL SPECIFIC LEVELS AND THEIR TERMS

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX : <http://www.semanticweb.org/SAMH/ontologies/>
SELECT ?SpecificLevel ?Term 
WHERE { ?SpecificLevel :hasTerm ?Term . }
ORDER BY ?SpecificLevel ?Term

//Patients of a Therapist

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX samh: <http://www.semanticweb.org/SAMH/ontologies/>

SELECT DISTINCT (?p as ?Patient)
WHERE {
	?t a samh:Transcription .
	?t samh:hasPatient ?p .
	?t samh:hasTherapist ?therapist
	filter( regex(str(?therapist), "Salgado"))
}
ORDER BY ?Patient

//Find Emotion Levels for term

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX samh: <http://www.semanticweb.org/SAMH/ontologies/>

SELECT DISTINCT ?pLevel ?gLevel ?intLevel ?spLevel
WHERE {
	?x a samh:Term . 
	{
		?spLevel samh:hasTerm ?x .
		?intLevel samh:isUpperLevelSpecific ?spLevel .
		?gLevel samh:isUpperLevelIntermediate ?intLevel .
		?pLevel samh:isUpperLevelGlobal ?gLevel .
	} 
	UNION
	 { 
		?pLevel samh:hasTerm ?x . 
		FILTER NOT EXISTS  { ?upperLevel samh:isUpperLevelSpecific ?pLevel . }
	}
	filter( regex(str(?x), "neutro"))
}

//Terms of Emotion Level

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX samh: <http://www.semanticweb.org/SAMH/ontologies/>

SELECT DISTINCT (?t as ?terms)
WHERE {
	{
		?eLevel samh:hasTerm ?t
	} 
	UNION
	{
		?eLevel samh:isUpperLevelSpecific ?spLevel .
		?spLevel samh:hasTerm ?t
	}
	UNION
	{
		?eLevel samh:isUpperLevelIntermediate ?intLevel .
		?intLevel samh:isUpperLevelSpecific ?spLevel .
		?spLevel samh:hasTerm ?t
	}
	UNION
	{
		?eLevel samh:isUpperLevelGlobal ?gLevel .
		?gLevel samh:isUpperLevelIntermediate ?intLevel .
		?intLevel samh:isUpperLevelSpecific ?spLevel .
		?spLevel samh:hasTerm ?t
	}
	filter( regex(str(?eLevel), "AMOR" ))
}
ORDER BY ?t


//Best Transcription for a Emotion Level

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX samh: <http://www.semanticweb.org/SAMH/ontologies/>

SELECT DISTINCT (?t as ?Transcription) ?score
WHERE
{ 
	?sr rdf:type samh:SearchResult .
	?sr samh:relatesToLevel ?l .
	?t samh:hasSearchResult ?sr .
	?sr samh:score ?score .
	filter( regex(str(?l), "AMOR" ))
}
ORDER BY DESC(?score)
LIMIT 1

//Emotions for a transcription

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX samh: <http://www.semanticweb.org/SAMH/ontologies/>

SELECT DISTINCT (?l as ?EmotionLevel) ?score
WHERE
{ 
	?sr rdf:type samh:SearchResult .
	?sr samh:relatesToLevel ?l .
	?t samh:hasSearchResult ?sr .
	?sr samh:score ?score .
	filter( regex(str(?t), "P003_13" ))
}
ORDER BY DESC(?score)

//Emotions for a Patient

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX samh: <http://www.semanticweb.org/SAMH/ontologies/>

SELECT DISTINCT (?l as ?EmotionLevel) (SUM(?score) as ?sum)
WHERE 
{
	{
 		SELECT DISTINCT ?t ?p
		WHERE
		{ 
			?t a samh:Transcription .
			?t samh:hasPatient ?p .
			filter( regex(str(?p), "P003" ))
		}
	}

	?sr rdf:type samh:SearchResult .
	?sr samh:relatesToLevel ?l .
	?t samh:hasSearchResult ?sr .
	?sr samh:score ?score .
}
GROUP BY ?l
ORDER BY DESC(?sum)